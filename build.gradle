// Copyright (c) 2017 Uber Technologies, Inc.
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
// THE SOFTWARE.

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath('io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3')
    }
}

plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id 'checkstyle'
    id 'jacoco'
    id 'com.kageiit.jacobo' version '2.0.5' apply false
    id 'net.researchgate.release' version '2.4.0' apply false
}

apply plugin: 'io.codearte.nexus-staging'

description = 'tally client'

// Remove default (empty) artifact
configurations.archives.artifacts.clear()

allprojects {
    group = 'com.uber.m3'
    version = '0.0.1-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'jacoco'
    apply plugin: 'com.kageiit.jacobo'
    apply plugin: 'net.researchgate.release'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile('junit:junit:4.12')
    }

    ext.ossrhUsername = System.getenv('OSSRH_JIRA_USERNAME')
    ext.ossrhPassword = System.getenv('OSSRH_JIRA_PASSWORD')
    ext."signing.keyId" = System.getenv('GPG_KEY_ID')
    ext."signing.password" = System.getenv('GPG_PASSPHRASE')
    ext."secretKeyRingFileName" = 'signing.gpg'

    if (ext."signing.keyId" && ext."signing.password" && ossrhUsername && ossrhPassword) {
        signing {
            sign configurations.archives
        }

        uploadArchives {
            repositories {
                mavenDeployer {
                    uniqueVersion = false

                    beforeDeployment {
                        MavenDeployment deployment -> signing.signPom(deployment)
                    }
                    repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    pom.project {
                        name project.getPath()
                        description project.description
                        packaging 'jar'
                        url 'https://github.com/uber-java/tally'

                        scm {
                            connection 'scm:git:https://github.com/uber-java/tally.git'
                            developerConnection 'scm:git:https://github.com/uber-java/tally.git'
                            url 'https://github.com/your/project.git'
                        }

                        licenses {
                            license {
                                name 'The MIT License (MIT)'
                                url 'https://github.com/uber-java/tally/blob/master/LICENSE.md'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id = 'juchan'
                                name = 'Justin Chan'
                                email = 'juchan@uber.com'
                            }
                        }
                    }

                }
            }
        }
    }

    // ----------------------
    // Build & Release
    // ----------------------
    task wrapper(type: Wrapper) {
        gradleVersion = '3.3'
    }

    release {
        tagTemplate = '$name-$version'
    }

    afterReleaseBuild.dependsOn uploadArchives
}

subprojects {
    apply plugin: 'checkstyle'

    checkstyle {
        configFile = file('../checkstyles/uber_checks.xml')
        toolVersion = '6.19'
    }

    ext."signing.secretKeyRingFile" = "../ci/${ext.secretKeyRingFileName}"

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar, javadocJar
    }

    // ----------------------
    // Testing
    // ----------------------
    task jacobo(type: com.kageiit.jacobo.JacoboTask) {
        jacocoReport = file("$buildDir/reports/jacoco/test/jacocoTestReport.xml")
        coberturaReport = file("$buildDir/reports/cobertura/cobertura.xml")
        srcDirs = sourceSets.main.java.srcDirs
        includeFileNames = []
    }

    // Generate unit test and test coverage reports
    jacocoTestReport.reports.xml.enabled = true
    jacobo.dependsOn jacocoTestReport
    test.finalizedBy jacobo
}

ext."signing.secretKeyRingFile" = "ci/${ext.secretKeyRingFileName}"

// ----------------------
// Fat jar task factories
// ----------------------

def fatJar(name, projects) {
    return tasks.create([
        name: "${name}Jar",
        type: Jar,
        dependsOn: projects.collect{it + ':compileJava'}
    ]) {
        baseName name
        from files(projects.collect{ project(it).sourceSets.main.output })
    }
}

def fatSourcesJar(name, projects) {
    return tasks.create([
        name: "${name}SourcesJar",
        type: Jar,
        dependsOn: projects.collect{it + ':classes'}
    ]) {
        classifier 'sources'
        baseName name
        from files(projects.collect{ project(it).sourceSets.main.allSource })
    }
}

def fatJavadocJar(name, projects) {
    def javadocTaskName = "${name}Javadoc"

    def fatJavadoc = tasks.create([
        name: javadocTaskName,
        type: Javadoc,
        dependsOn: projects.collect{it + ':classes'}
    ]) {
        source = projects.collect{ project(it).sourceSets.main.allSource }
    }

    return tasks.create([
        name: "${name}JavadocJar",
        type: Jar,
        dependsOn: ":${javadocTaskName}"
    ]) {
        classifier 'javadoc'
        baseName name
        from files(fatJavadoc.destinationDir)
    }
}

// --------------------------------------
// StatsD fat jar
// --------------------------------------

def statsd_fat_jar_name = 'tally-statsd-fat'
def statsd_fat_jar_projs = [':tally-core', ':tally-statsd']

// -------------------
// Fat jars to publish
// -------------------
artifacts {
    archives fatJar(statsd_fat_jar_name, statsd_fat_jar_projs),
        fatSourcesJar(statsd_fat_jar_name, statsd_fat_jar_projs),
        fatJavadocJar(statsd_fat_jar_name, statsd_fat_jar_projs)
}

nexusStaging {}

// --------------------------------------
// Testing and coverage for whole project
// --------------------------------------
task jacocoRootReport(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
    dependsOn = subprojects.test
    additionalSourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories =  files(subprojects.sourceSets.main.output)
    executionData = files(subprojects.jacocoTestReport.executionData)
    reports.xml.enabled = true
}

task jacobo(type: com.kageiit.jacobo.JacoboTask) {
    jacocoReport = file("$buildDir/reports/jacoco/jacocoRootReport/jacocoRootReport.xml")
    coberturaReport = file("$buildDir/reports/cobertura/cobertura.xml")
    srcDirs = files(subprojects.sourceSets.main.java.srcDirs)
    includeFileNames = []
}

jacobo.dependsOn jacocoRootReport
test.finalizedBy jacobo
